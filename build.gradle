buildscript {
    ext.kotlin_version = '1.1.51'
    ext.dokka_version = '0.9.15'

    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    }
}

plugins {
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.9.9"
}

group 'com.github.fhermansson'
version '1.1.0'

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    implementation("org.assertj:assertj-assertions-generator:2.0.0") {
        exclude module: 'logback-classic'
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

dokka {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/javadoc"
}

gradlePlugin {
    plugins {
        assertjGenerator {
            id = 'com.github.fhermansson.assertj-generator'
            implementationClass = 'com.github.fhermansson.gradle.assertj.plugin.AssertjGeneratorPlugin'
        }
    }
}
pluginBundle {
    website = 'https://github.com/fhermansson/assertj-generator-gradle-plugin'
    vcsUrl = 'https://github.com/fhermansson/assertj-generator-gradle-plugin'
    description = 'Generate Assertj assertion classes.'
    tags = ['code-generation', 'assertj', 'java']

    plugins {
        assertjGenerator {
            id = 'com.github.fhermansson.assertj-generator'
            displayName = 'Assertj Generator plugin'
        }
    }
}


task dokkaJar(type: Jar) {
    group = 'documentation'
    classifier = 'javadoc'
    from(dokka)
}
install.dependsOn dokkaJar
publishPlugins.dependsOn dokkaJar

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3'
}
